HAI 1.4
  CAN HAS STDIO?
  CAN HAS STRING?

  I HAS A FILENAME
  I HAS A PART

  GIMMEH FILENAME
  GIMMEH PART

  HOW IZ I READLINE YR FILE
    I HAS A LINE ITZ ""
    IM IN YR LOOP
      I HAS A CHAR ITZ I IZ STDIO'Z LUK YR FILE AN YR 1 MKAY
      BOTH SAEM CHAR AN ":)"
      O RLY?
        YA RLY, FOUND YR LINE
      MEBBE BOTH SAEM CHAR AN ""
        FOUND YR FAIL
      NO WAI
        LINE R SMOOSH LINE AN CHAR MKAY
      OIC
    IM OUTTA YR LOOP
  IF U SAY SO

  HOW IZ I SPLIT YR STR
    I HAS A ANSER ITZ A BUKKIT
    I HAS A NUM ITZ 0
    I HAS A PART ITZ ""
    IM IN YR LOOP UPPIN YR INDEX
      I HAS A CHAR ITZ I IZ STRING'Z AT YR STR AN YR INDEX MKAY
      BOTH SAEM CHAR AN ""
      O RLY?, YA RLY
        ANSER HAS A SRS NUM ITZ PART
        FOUND YR ANSER
      MEBBE BOTH SAEM CHAR AN " "
        I HAS A B4 ITZ I IZ STRING'Z AT YR STR AND YR DIFF OF INDEX AN 1 MKAY
        NOT BOTH SAEM B4 AN " ", O RLY?, YA RLY
          ANSER HAS A SRS NUM ITZ PART
          NUM R SUM OF NUM AN 1
          PART R ""
        OIC
      NO WAI
        PART R SMOOSH PART AN CHAR MKAY
      OIC
    IM OUTTA YR LOOP
  IF U SAY SO

  HOW IZ I SORTIN YR LIZT
    I HAS A LEN ITZ 0
    IM IN YR LOOP UPPIN YR CHECK
      BOTH SAEM LIZT'Z SRS CHECK AN FAIL, O RLY?
        YA RLY, GTFO
      OIC
      LEN R SUM OF LEN AN 1
    IM OUTTA YR LOOP
    I IZ KWIKING YR LIZT AN YR 0 AN YR DIFF OF LEN AN 1 MKAY
  IF U SAY SO

  HOW IZ I KWIKING YR LIZT AN YR LO AN YR HI
    BOTH SAEM LO AN BIGGR OF LO AN HI, O RLY?
      YA RLY, FOUND YR 1337
    OIC
    DIFFRINT LO AN BIGGR OF LO AN 0, O RLY?
      YA RLY, FOUND YR 1337
    OIC
    I HAS A P ITZ I IZ PARTISHUN YR LIZT AN YR LO AN YR HI MKAY
    I IZ KWIKING YR LIZT AN YR LO AN YR DIFF OF P AN 1 MKAY
    I IZ KWIKING YR LIZT AN YR SUM OF P AN 1 AN YR HI MKAY
  IF U SAY SO

  HOW IZ I PARTISHUN YR LIZT AN YR LO AN YR HI
    I HAS A PIVOT ITZ LIZT'Z SRS HI
    I HAS A I ITZ LO
    I HAS A J ITZ LO
    IM IN YR LOOP
      BOTH SAEM HI AN J, O RLY?
        YA RLY, GTFO
        MEBBE BOTH SAEM LIZT'Z SRS J AN SMALLR OF LIZT'Z SRS J AN PIVOT
          I HAS A TEMP ITZ LIZT'Z SRS I
          LIZT'Z SRS I R LIZT'Z SRS J
          LIZT'Z SRS J R TEMP
          I R SUM OF I AN 1
      OIC
      J R SUM OF J AN 1
    IM OUTTA YR LOOP
    I HAS A TEMP ITZ LIZT'Z SRS I
    LIZT'Z SRS I R LIZT'Z SRS HI
    LIZT'Z SRS HI R TEMP
    FOUND YR I
  IF U SAY SO

  HOW IZ I COUNTIN YR LIZT AN YR SKALAR
    I HAS A NUMM ITZ 0
    IM IN YR LOOP UPPIN YR CHECK
      BOTH SAEM LIZT'Z SRS CHECK AN FAIL, O RLY?
        YA RLY, FOUND YR NUMM
      MEBBE BOTH SAEM LIZT'Z SRS CHECK AN SKALAR
        NUMM R SUM OF NUMM AN 1
      OIC
    IM OUTTA YR LOOP
  IF U SAY SO

  I HAS A FILE ITZ I IZ STDIO'Z OPEN YR FILENAME AN YR "r" MKAY
  I HAS A LIZT ITZ A BUKKIT
  I HAS A LIZZT ITZ A BUKKIT

  IM IN YR LOOP UPPIN YR CHECK
    I HAS A LINE ITZ I IZ READLINE YR FILE MKAY
    BOTH SAEM LINE AN FAIL, O RLY?, YA RLY
      LIZT HAS A SRS CHECK ITZ FAIL
      LIZZT HAS A SRS CHECK ITZ FAIL
      GTFO
    OIC
    I HAS A NUMS ITZ I IZ SPLIT YR LINE MKAY
    LIZT HAS A SRS CHECK ITZ MAEK NUMS'Z SRS 0 NUMBR
    LIZZT HAS A SRS CHECK ITZ MAEK NUMS'Z SRS 1 NUMBR
  IM OUTTA YR LOOP
  I IZ STDIO'Z CLOSE YR FILE MKAY

  I HAS A SUMM ITZ 0
  BOTH SAEM PART AN "1"
  O RLY?, YA RLY
    I IZ SORTIN YR LIZT MKAY
    I IZ SORTIN YR LIZZT MKAY
    IM IN YR LOOP UPPIN YR LOOPS
      BOTH SAEM LIZT'Z SRS LOOPS AN FAIL, O RLY?
        YA RLY, GTFO
      OIC
      I HAS A BIGGUN ITZ BIGGR OF LIZT'Z SRS LOOPS AN LIZZT'Z SRS LOOPS
      I HAS A SMALLUN ITZ SMALLR OF LIZT'Z SRS LOOPS AN LIZZT'Z SRS LOOPS
      SUMM R SUM OF SUMM AN DIFF OF BIGGUN AN SMALLUN
      BTW VISIBLE LIZZT'Z SRS LOOPS
    IM OUTTA YR LOOP
  NO WAI
    IM IN YR LOOP UPPIN YR CHECK
      BOTH SAEM LIZT'Z SRS CHECK AN FAIL, O RLY?
        YA RLY, GTFO
      OIC
      I HAS A V ITZ LIZT'Z SRS CHECK
      SUMM R SUM OF SUMM AN PRODUKT OF V AN I IZ COUNTIN YR LIZZT AN YR V MKAY
    IM OUTTA YR LOOP
  OIC

  VISIBLE SUMM

KTHXBYE
